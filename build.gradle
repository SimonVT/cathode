buildscript {
  apply from: rootProject.file('deps.gradle')

  repositories {
    google()
    mavenCentral()
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
  }

  dependencies {
    classpath deps.androidPlugin
    classpath deps.kotlin.plugin
    classpath deps.kotlin.ktlint
    classpath deps.google.services
    classpath deps.firebase.fabric
    classpath deps.butterknifeGradle
  }
}

allprojects {
  repositories {
    google()
    mavenCentral()
    jcenter()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
  }
}

ext {
  versionMajor = 7
  versionMinor = 0
  versionPatch = 8

  versionCode = versionMajor * 10000 + versionMinor * 100 + versionPatch
  versionName = versionMajor + "." + versionMinor + "." + versionPatch

  minSdkVersion = 21
  targetSdkVersion = 29

  compileSdkVersion = 29
  buildToolsVersion = '29.0.3'
}

def getProperty(String propertyFile, String field) {
  def rootDir = project.rootDir
  def localProperties = new File(rootDir, propertyFile + ".properties")
  if (localProperties.exists()) {
    Properties properties = new Properties()
    localProperties.withInputStream { instr -> properties.load(instr)
    }
    def value = properties.getProperty(field)
    return value
  }

  return ""
}

apply from: rootProject.file('deps.gradle')
