apply plugin: 'com.android.library'
apply plugin: 'checkstyle'
apply plugin: 'com.jakewharton.butterknife'

dependencies {
  implementation deps.appcompat
  implementation deps.butterknife
  annotationProcessor deps.butterknifeCompiler
  implementation deps.dagger
  implementation deps.androidDesign
  implementation deps.supportPalette
  implementation deps.picasso
  implementation deps.recyclerView
  implementation deps.schematic
  implementation deps.supportAnnotations
  implementation deps.supportFragment
  implementation deps.timber

  testImplementation deps.junit
  testImplementation deps.robolectric
  testImplementation deps.truth
}

android {
  compileSdkVersion parent.ext.compileSdkVersion
  buildToolsVersion parent.ext.buildToolsVersion

  defaultConfig {
    minSdkVersion parent.ext.minSdkVersion
    targetSdkVersion parent.ext.targetSdkVersion

    versionCode = parent.ext.versionCode;
    versionName = parent.ext.versionName
  }

  lintOptions {
    abortOnError false
  }
}

checkstyle {
  configFile project.file('../config/checkstyle/checkstyle.xml')
  showViolations true
}

android.libraryVariants.all { variant ->
  def name = variant.buildType.name

  def checkstyle = project.tasks.create "checkstyle${name.capitalize()}", Checkstyle
  checkstyle.dependsOn variant.javaCompile
  checkstyle.source variant.javaCompile.source
  checkstyle.classpath = project.fileTree(variant.javaCompile.destinationDir)
  checkstyle.exclude('**/BuildConfig.java')
  checkstyle.exclude('**/R.java')
  checkstyle.exclude('**/R2.java')
  project.tasks.getByName("check").dependsOn checkstyle
}

apply from: rootProject.file('deps.gradle')
