plugins {
  id 'com.android.library'
  id 'kotlin-android'
  id 'kotlin-kapt'
  id 'org.jlleitschuh.gradle.ktlint'
  id 'checkstyle'
}

dependencies {
  implementation project(':cathode-common')
  implementation project(':cathode-provider')
  implementation deps.kotlin.stdlib
  implementation deps.dagger
  implementation deps.daggerAndroid
  implementation deps.daggerAndroidSupport
  kapt deps.daggerCompiler
  kapt deps.daggerAndroidApt
  implementation deps.gson
  implementation deps.schematic
  kapt deps.schematicCompiler
  implementation deps.androidx.annotations
  implementation deps.timber
}

android {
  compileSdkVersion parent.ext.compileSdkVersion
  buildToolsVersion parent.ext.buildToolsVersion

  defaultConfig {
    minSdkVersion parent.ext.minSdkVersion
    targetSdkVersion parent.ext.targetSdkVersion

    versionCode = parent.ext.versionCode;
    versionName = parent.ext.versionName
  }

  viewBinding {
    enabled = true
  }

  lintOptions {
    abortOnError false
  }
}

checkstyle {
  configFile project.file('../config/checkstyle/checkstyle.xml')
  showViolations true
}

android.libraryVariants.all { variant ->
  def name = variant.buildType.name

  def checkstyle = project.tasks.create "checkstyle${name.capitalize()}", Checkstyle
  checkstyle.dependsOn variant.javaCompile
  checkstyle.source variant.javaCompile.source
  checkstyle.classpath = project.fileTree(variant.javaCompile.destinationDir)
  checkstyle.exclude('**/BuildConfig.java')
  checkstyle.exclude('**/R.java')
  project.tasks.getByName("check").dependsOn checkstyle
}

apply from: rootProject.file('deps.gradle')
