plugins {
  id 'com.android.library'
  id 'kotlin-android'
  id 'kotlin-kapt'
  id 'org.jlleitschuh.gradle.ktlint'
  id 'checkstyle'
}
dependencies {
  implementation project(':cathode-common')
  implementation project(':cathode-entity')
  implementation project(':cathode-settings')
  implementation project(':trakt-api')
  implementation deps.kotlin.stdlib
  implementation deps.dagger
  kapt deps.daggerCompiler
  implementation deps.schematic
  kapt deps.schematicCompiler
  implementation deps.timber
  implementation deps.androidx.loader

  testImplementation deps.junit
  testImplementation deps.robolectric
  testImplementation deps.truth
  testImplementation deps.androidx.test.core
  testImplementation deps.androidx.test.coreKtx
  testImplementation deps.dagger
  testAnnotationProcessor deps.daggerCompiler
}

android {
  compileSdkVersion parent.ext.compileSdkVersion
  buildToolsVersion parent.ext.buildToolsVersion

  defaultConfig {
    minSdkVersion parent.ext.minSdkVersion
    targetSdkVersion parent.ext.targetSdkVersion

    versionCode = parent.ext.versionCode;
    versionName = parent.ext.versionName
  }

  lintOptions {
    abortOnError false
  }

  buildTypes {
    debug {
      buildConfigField "String", "PROVIDER_AUTHORITY",
          "\"net.simonvt.cathode.debug.provider.CathodeProvider\""
      resValue "string", "authority_provider", "net.simonvt.cathode.debug.provider.CathodeProvider"
    }
    release {
      buildConfigField "String", "PROVIDER_AUTHORITY",
          "\"net.simonvt.cathode.provider.CathodeProvider\""
      resValue "string", "authority_provider", "net.simonvt.cathode.provider.CathodeProvider"
    }
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }
}

checkstyle {
  configFile project.file('../config/checkstyle/checkstyle.xml')
  showViolations true
}

android.libraryVariants.all { variant ->
  def name = variant.buildType.name

  def checkstyle = project.tasks.create "checkstyle${name.capitalize()}", Checkstyle
  checkstyle.dependsOn variant.javaCompile
  checkstyle.source variant.javaCompile.source
  checkstyle.classpath = project.fileTree(variant.javaCompile.destinationDir)
  checkstyle.exclude('**/BuildConfig.java')
  checkstyle.exclude('**/R.java')
  project.tasks.getByName("check").dependsOn checkstyle
}

apply from: rootProject.file('deps.gradle')
